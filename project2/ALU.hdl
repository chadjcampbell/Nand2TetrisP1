// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //check for zerox
    Mux16(a=x, b=false, sel=zx, out=zerox);
    //make a notx and check for notx
    Not16(in=zerox, out=notx);
    Mux16(a=zerox, b=notx, sel=nx, out=donex);

    //check for zeroy
    Mux16(a=y, b=false, sel=zy, out=zeroy);
    //make a noty and check for noty
    Not16(in=zeroy, out=noty);
    Mux16(a=zeroy, b=noty, sel=ny, out=doney);

    //compute + or &
    Add16(a=donex, b=doney, out=addedxy);
    And16(a=donex, b=doney, out=andedxy);
    Mux16(a=andedxy, b=addedxy, sel=f, out=andoraddxy);

    //check if output notted (share split out bits for zero check, and MSB for negative check)
    Not16(in=andoraddxy, out=notOut);
    Mux16(a=andoraddxy, b=notOut, sel=no, out=out, out[15]=negCheck, out[0..7]=firstHalfZeroCheck, out[8..15]=secondHalfZeroCheck);

    //check zeroOut
    Or8Way(in=firstHalfZeroCheck, out=zck1);
    Or8Way(in=secondHalfZeroCheck, out=zck2);
    Or(a=zck1, b=zck2, out=bothHalfZeroCheck);
    Not(in=bothHalfZeroCheck, out=zr);
    

    //check negative
    Mux(a=false, b=true, sel=negCheck, out=ng);
    
}